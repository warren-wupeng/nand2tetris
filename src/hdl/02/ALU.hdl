/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   Mux16(a=x, b[0..15]=false, sel=zx, out=zxOut);
   Not16(in=zxOut, out=nxOut);
   Mux16(a=zxOut, b=nxOut, sel=nx, out=nzxOut);

   Mux16(a=y, b[0..15]=false, sel=zy, out=zyOut);
   Not16(in=zyOut, out=nyOut);
   Mux16(a=zyOut, b=nyOut, sel=ny, out=nzyOut);

   And16(a=nzxOut, b=nzyOut, out=nzxAndNzyOut);
   Add16(a=nzxOut, b=nzyOut, out=nzxAddNzyOut);
   Mux16(a=nzxAndNzyOut, b=nzxAddNzyOut, sel=f, out=fOut);

   Not16(in=fOut, out=nfOut);
   Mux16(a=fOut, b=nfOut, sel=no, out=out, out[0..7]=outLow, out[8..15]=outHigh, out[15]=ng);

   Or8Way(in=outLow, out=orLowOut);
   Or8Way(in=outHigh, out=orHighOut);
   Or(a=orLowOut, b=orHighOut, out=not0);
   Mux(a=true, b=false, sel=not0, out=zr);

}